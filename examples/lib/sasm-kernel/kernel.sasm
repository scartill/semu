/// Kernel functions

STRUCT TCB
    DW sp
    DW next
    DW prev
END

// Running thread pointer
DW runpt

// Create idle thread
DS TCB idletcb
DW idlestack*100

FUNC CreateIdle
    ldr &idlestack a
    ldr &TIdle b
    ldc 0 c
    CALL InitStack      // a <- SP
    
    ldr &idletcb b
    PTR b sp#TCB h      // idletcb.sp = a
    mrm a h
    PTR b next#TCB h    // idletcb.next = &idletcb
    mrm b h
    PTR b prev#TCB h    // idletcb.prev = &idletcb
    mrm b h
    ldr &runpt h        // runpt = &idletcb
    mrm b h
    ret                 // return a

// System idle thread
FUNC TIdle
    nop
    ldc 73 a            // a <- 'I'
    CALL api::Print    
    CALL api::Suspend
    ldr &TIdle c
    jmp c

FUNC Startup    
    CALL SetupHandlers
    CALL CreateIdle
    
    CALL app::Start
    
    // Return idle thread stack pointer    
    ldr &runpt a
    RPTR a sp#TCB b
    mmr b a             // return runpt->sp
    ret

FUNC CreateThread // stack_top, func, param, tcb
    push d
    CALL InitStack             // a <- new_stack
    pop b                      // b <- tcb
    
    PTR b sp#TCB h
    mrm a h                    // tcp.sp = a
      
    ldr &runpt d               // d <- runpt
    mmr d d                    // d <- *runpt
    
    PTR d next#TCB h
    mmr h e                    // e <- runpt->next
    
    mrm b h                    // runpt->next = tcb
    
    PTR b next#TCB h    
    mrm e h                    // tcp.next = e
    
    PTR b prev#TCB h
    mrm d h                    // tcp.prev = *runpt
    
    RPTR e prev#TCB h
    mrm b h                    // runpt->next.prev = tcb    
    
    ret

// Init stack for a new thread
FUNC InitStack // stack_top, func, param -> new_stack
    mrm b a             // Thread start = <func>
    ldc 4 d
    add a d a
    mrm c a             // Func <param> passed in a
    ldc 32 d
    add a d a
    ret
    
// Setup interrupt handlers
FUNC SetupHandlers
    // Loopback handler
    ldr &HLoopback a
    ldc 0 b     // int 0 handler address
    mrm a b     // Handler = loopback

    // Setup timer
    ldr &HScheduler a
    ldc 4 b     // int 1 handler address
    mrm a b     // Handler = scheduler
    ldc 1 a     // enable == 1
    ldc 1 b     // serial on line 1
    out a b     // Enable timer
    
    ret

// User interrupt handler - send 'word' to serial
// input: a - symbol to 'out'. 0 for force timer to suspend
FUNC HLoopback
  suspend:
    ldr &serial b
    jgt a b
    ldr &HScheduler b       // NB: no 'cll' instruction
    jmp b                   // - saving the stack intact
    
  serial:
    ldc 2 b
    out a b
    irx

// Timer tick handler - schedule the next thread
FUNC HScheduler
    ldr &runpt a
    
    // Save stack pointer
    RPTR a sp#TCB b
    ssp c
    mrm c b         // runpt->sp = sp

    RPTR a next#TCB b
    mmr b e         // e = runpt->next

    // Update running TCB
    mrm e a

    // Fetch new stack pointer    
    RPTR a sp#TCB b
    mmr b c
    lsp c

    // Resume that thread
    irx    


